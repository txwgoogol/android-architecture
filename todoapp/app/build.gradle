apply plugin: 'com.android.application' //声明是Android程序
apply plugin: 'org.greenrobot.greendao'

android {

    compileSdkVersion 27 //编译sdk的版本，也就是API Level，例如API-15、API-20、API-27等等。

    //关闭Android Studio的PNG合法性检查的
    //aaptOptions.cruncherEnabled = false
    //aaptOptions.useNewCruncher = false

    defaultConfig {

        applicationId "com.txwgoogol.top.weather.todomvp" //应用包名

        //最小sdk版本，如果设备小于这个版本或者大于maxSdkVersion(一般不用)将无法安装这个应用
        minSdkVersion 16

        //目标sdk版本，如果设备等于这个版本那么android平台就不进行兼容性检查，运行效率会高一点
        targetSdkVersion 27

        //版本更新了几次，第一版应用是1，以后每更新一次加1
        versionCode 1

        //版本信息，这个会显示给用户，就是用户看到的版本号
        versionName "1.0"

        //指定打包成Jar文件时候的文件名称
        //archivesBaseName = "weshare-$versionName"

        //vector 图片使用配置
        vectorDrawables.useSupportLibrary = true

        //单元测试配置
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //压缩资源方法来瘦身APK 只保留中文和英文语言资源
        resConfigs "en", "zh"
        //resConfigs "hdpi","xhdpi","xxhdpi","xxxhdpi"

        //jni
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        ndk {
            moduleName = "weather" //设置库(so)文件名称

            //ldLibs "log", "z", "m", "jnigraphics", "android" //引入库，比如要用到的__android_log_print
            //abiFilters "armeabi", "x86", "armeabi-v7a"      //'armeabi' , 'x86' , 'armeabi-v7a' 显示指定支持的ABIs
            //cFlags "-std=c++11 -fexceptions"                // C++11
            //stl "gnustl_static"
        }

        //当方法数超过65535(方法的索引使用的是一个short值，而short最大值是65535)的时候允许打包成多个dex文件，动态加载dex。这里面坑很深啊
        //multiDexEnabled true

    }

    //multiDex的一些相关配置，这样配置可以让你的编译速度更快
    //dexOptions {
    //    preDexLibraries = false //让它不要对Lib做preDexing
    //    incremental true //开启incremental dexing,优化编译效率，这个功能android studio默认是关闭的。
    //    javaMaxHeapSize "4g"     //增加java堆内存大小
    //}

    //jni 配置文件
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    //签名信息配置
    signingConfigs {
        //调试版本
        debug {
            storeFile file("debug/debug.keystore") //签名文件
            storePassword "android" //签名密码
            keyAlias "androiddebugkey" //签名别名
            keyPassword "android"  //别名密码
        }
        //发布版本
        release {
            //storeFile file("release/mygirlweather.jks")      //签名文件
            //storePassword "******"
            //keyAlias "******"
            //keyPassword "******"  //签名密码
        }
    }

    buildTypes {

        release { //release版本的配置
            shrinkResources true //移除无用的resource文件 https://www.jianshu.com/p/46560895b42c 缩减代码和优化
            zipAlignEnabled true //是否支持zip 默认为true
            //https://blog.csdn.net/hello_1995/article/details/52870573 让应用程序和整个系统运行得更快
            minifyEnabled true //是否进行混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //release的Proguard默认为Module下的proguard-rules.pro文件.

            // 自定义输出配置
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def fileName = "${variant.productFlavors[0].name}_weather_" + "${variant.versionName}_release.apk"
                    def outFile = output.outputFile
                    if (outFile != null && outFile.name.endsWith('.apk')) {
                        outputFileName = fileName
                    }
                }
            }

            //jniDebuggable false  //关闭jni调试
        }

    }

    flavorDimensions "productFlavors"

    /**
     * 配置渠道
     * gradle 命令行打包配置
     * https://www.jianshu.com/p/49e8a148e4e0
     */
    productFlavors {
        //在这里你可以设置你的产品发布的一些东西，比如你现在的软件需要发布到不同渠道，且不同渠道中的包名不同，那么可以在此进行配置；
        //甚至可以设置不同的 AndroidManifest.xml 文件。
        googleplay {} //Google Play 商店
        yingyongbao {} //腾讯 应用宝
        qh360{} //360手机助手
        xiaomi {} //小米应用商店
        huawei{} //华为应用市场
        baidu{} //百度手机助手
        ali{} //阿里应用分发开放平台
        zs91{} //智度公司公司 91助手
        wandoujia{} //北京卓易讯畅科技有限公司 豌豆荚
        anzhi{} //北京力天无限网络技术有限公司 安智应用商店
        liqu{} //北京历趣科技有限公司 历趣应用商店
        wo{} //小沃科技有限公司 沃应用商店
    }

    productFlavors.all {
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    //在这里你可以进行 Java 的版本配置，以便使用对应版本的一些新特性
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //程序在编译的时候会检查lint，有任何错误提示会停止build，我们可以关闭这个开关
    lintOptions {
        checkReleaseBuilds false //即使报错也不会停止打包
        abortOnError false //打包release版本的时候进行检测
    }

}

//一些依赖的框架
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //android support
    implementation 'com.android.support.constraint:constraint-layout:1.1.1'
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    //test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    //square
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    //reactivex
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    //json analysis
    implementation 'com.google.code.gson:gson:2.8.5'
    //image load
    //implementation 'com.github.bumptech.glide:glide:4.6.1'
    //annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
    //annotation
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //greenrobot
    //implementation 'org.greenrobot:greendao:3.2.2'
    //circle layout
    //implementation 'de.hdodenhof:circleimageview:2.2.0'
    //eventbus
    //implementation 'org.greenrobot:eventbus:3.1.1'
    //rxjava permission jar
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    //refresh layout
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-7'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-7'
    //search view
    implementation 'com.miguelcatalan:materialsearchview:1.4.0'
    //Amap location
    implementation 'com.amap.api:location:latest.integration'
    //module
    implementation project(':widget')
    implementation project(':searchview')
    implementation 'org.jetbrains:annotations:15.0'
    implementation 'com.orhanobut:logger:2.2.0'
    //ui recyclerview adapter
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.40'
    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'
    //umeng
    implementation 'com.umeng.sdk:common:1.5.0'
    implementation 'com.umeng.sdk:analytics:7.5.0'
}

//gradle打包参考 https://blog.csdn.net/baidu_31093133/article/details/51860637